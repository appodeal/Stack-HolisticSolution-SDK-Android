List<Project> collectServices() {
    return collectProjects(':services', null, null)
}

List<Project> collectConnectors() {
    return collectProjects(':connectors', null, null)
}

List<Project> collectRegulators() {
    return collectProjects(':regulators', null, null)
}

private List<Project> collectProjects(String rootProject,
                                      List<String> including,
                                      List<String> excluding) {
    List<Project> resultProjects = new ArrayList<>()
    project(rootProject).childProjects.values().forEach { project ->
        if (including == null || including.contains(project.name)) {
            if (excluding == null || !excluding.contains(project.name)) {
                resultProjects.add(project)
            }
        }
    }
    return resultProjects
}

String obtainAarVersion(Project targetProject) {
    if (targetProject.name == project(':sdk').name
            || targetProject.name == project(':sdk-full').name) {
        return "${obtainVersion(targetProject)}"
    }
    return obtainVersion(':sdk') + "." + obtainVersion(targetProject)
}

String obtainVersion(String projectName) {
    return obtainVersion(project(projectName))
}

static String obtainVersion(Project targetProject) {
    return targetProject.android.defaultConfig.versionName
}

ext {
    collectServices = this.&collectServices
    collectConnectors = this.&collectConnectors
    collectRegulators = this.&collectRegulators
    obtainAarVersion = this.&obtainAarVersion
    obtainVersion = this.&obtainVersion
}