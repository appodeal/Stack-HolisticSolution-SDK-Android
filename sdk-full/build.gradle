import groovy.json.JsonOutput

apply plugin: 'com.android.library'
apply from: "${rootDir}/default.gradle"

ext {
    publishGroupId = "com.explorestack.hs"
    publishArtifactId = "sdk"
}

android {
    defaultConfig {
        versionCode 0
        versionName obtainVersion(':sdk') + ".${versionCode}"
    }
}

task prepareForPublishing(dependsOn: assemble) {
    doLast {
        delete publishOutputDir
        publishOutputDir.mkdirs()

        List<Project> hsComponentProjects = [project(':sdk')]
        collectServices().forEach { project ->
            hsComponentProjects.add(project)
        }
        collectConnectors().forEach { project ->
            hsComponentProjects.add(project)
        }
        collectRegulators().forEach { project ->
            hsComponentProjects.add(project)
        }

        def dependencies = []
        hsComponentProjects.forEach { project ->
            def dependency = [
                    groupId   : project.group,
                    artifactId: project.name,
                    version   : obtainHSComponentAarVersion(project)
            ]
            dependencies.push(dependency)
        }
        publishOutputDependenciesJson.createNewFile()
        publishOutputDependenciesJson.write(JsonOutput.toJson(dependencies))
        copy {
            from "$buildDir/outputs/aar/${project.name}-publish.aar"
            into publishOutputDir
        }
    }
}

task uploadToArtifactory(group: PUBLISH_GROUP) {
    dependsOn prepareForPublishing
    finalizedBy artifactoryPublish
}

task uploadToMavenLocal(group: PUBLISH_GROUP) {
    dependsOn prepareForPublishing
    finalizedBy publishToMavenLocal
}